#mapping
SELECT ?yr WHERE 
{
  ?journal rdf:type bench:Journal .
  ?journal dc:title "Journal 1 (1940)" .
  ?journal dcterms:issued ?yr .
}
#end
#mapping
SELECT ?inproc ?author ?booktitle ?title ?proc ?ee ?page ?url ?yr ?abstract WHERE 
{
  ?inproc rdf:type bench:Inproceedings .
  ?inproc dc:creator ?author .
  ?inproc bench:booktitle ?booktitle .
  ?inproc dc:title ?title .
  ?inproc dcterms:partOf ?proc .
  ?inproc rdfs:seeAlso ?ee .
  ?inproc swrc:pages ?page .
  ?inproc foaf:homepage ?url .
  ?inproc dcterms:issued ?yr .
  OPTIONAL 
  { 
    ?inproc bench:abstract ?abstract . 
  }
}
ORDER BY ?yr
#end
#mapping
SELECT ?article WHERE 
{
  ?article rdf:type bench:Article .
  ?article swrc:pages ?value .
}
#end
#mapping
SELECT ?article WHERE 
{
  ?article rdf:type bench:Article .
  ?article swrc:month ?value .
}
#end
#mapping
SELECT ?article WHERE 
{
  ?article rdf:type bench:Article .
  ?article swrc:isbn ?value .
}
#end
#mapping
SELECT DISTINCT ?name1 ?name2 WHERE 
{
  ?article1 rdf:type bench:Article .
  ?article2 rdf:type bench:Article .
  ?article1 dc:creator ?author1 .
  ?author1 foaf:name ?name1 .
  ?article2 dc:creator ?author2 .
  ?author2 foaf:name ?name2 .
  ?article1 swrc:journal ?journal .
  ?article2 swrc:journal ?journal .
  FILTER ( ?name1 < ?name2 )
}
#end
#mapping
SELECT DISTINCT ?person ?name WHERE 
{
  ?article rdf:type bench:Article .
  ?article dc:creator ?person .
  ?inproc rdf:type bench:Inproceedings .
  ?inproc dc:creator ?person2 .
  ?person foaf:name ?name .
  ?person2 foaf:name ?name2 .
  FILTER ( ?name = ?name2 ) 
}
#end
#mapping
SELECT DISTINCT ?person ?name WHERE 
{
  ?article rdf:type bench:Article .
  ?article dc:creator ?person .
  ?inproc rdf:type bench:Inproceedings .
  ?inproc dc:creator ?person .
  ?person foaf:name ?name .
}
#end
#mapping
SELECT ?yr ?name ?document WHERE 
{
  ?class rdfs:subClassOf foaf:Document .
  ?document rdf:type ?class .
  ?document dcterms:issued ?yr .
  ?document dc:creator ?author .
  ?author foaf:name ?name .
  OPTIONAL 
  {
    ?class2 rdfs:subClassOf foaf:Document .
    ?document2 rdf:type ?class2 .
    ?document2 dcterms:issued ?yr2 .
    ?document2 dc:creator ?author2 .
    FILTER (?author = ?author2 && ?yr2 < ?yr)
  } FILTER (!bound(?author2))
}
#end
#mapping
SELECT DISTINCT ?title WHERE 
{
  ?class rdfs:subClassOf foaf:Document .
  ?doc rdf:type ?class .
  ?doc dc:title ?title .
  ?bag2 ?member2 ?doc .
  ?doc2 dcterms:references ?bag2 .
  OPTIONAL 
  {
    ?class3 rdfs:subClassOf foaf:Document .
    ?doc3 rdf:type ?class3 .
    ?doc3 dcterms:references ?bag3 .
    ?bag3 ?member3 ?doc .
    OPTIONAL 
    {
      ?class4 rdfs:subClassOf foaf:Document .
      ?doc4 rdf:type ?class4 .
      ?doc4 dcterms:references ?bag4 .
      ?bag4 ?member4 ?doc3 .
    } FILTER (!bound(?doc4))
  } FILTER (!bound(?doc3))
}
#end
#mapping
SELECT DISTINCT ?name WHERE 
{
  ?erdoes rdf:type foaf:Person .
  ?erdoes foaf:name "Paul Erdoes" .
  {
    ?document dc:creator ?erdoes .
    ?document dc:creator ?author .
    ?document2 dc:creator ?author .
    ?document2 dc:creator ?author2 .
    ?author2 foaf:name ?name .
    FILTER (?author != ?erdoes && ?document2 != ?document && ?author2 != ?erdoes && ?author2 != ?author)
  } 
  UNION 
  {
    ?document dc:creator ?erdoes .
    ?document dc:creator ?author .
    ?author foaf:name ?name .
    FILTER (?author != ?erdoes)
  }
}
#end
#mapping
SELECT DISTINCT ?predicate WHERE 
{
  {
    ?person rdf:type foaf:Person .
    ?subject ?predicate ?person .
  } 
  UNION 
  {
    ?person rdf:type foaf:Person .
    ?person ?predicate ?object .
  }
}
#end
#mapping
SELECT ?subject ?predicate WHERE 
{
  ?subject ?predicate person:Paul_Erdoes .
}
#end
#mapping
SELECT ?ee WHERE 
{
  ?publication rdfs:seeAlso ?ee .
}
ORDER BY ?ee
LIMIT 10
OFFSET 50
#end